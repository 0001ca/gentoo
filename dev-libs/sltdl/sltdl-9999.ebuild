# Copyright 1999-2025 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

inherit edo toolchain-funcs

# git.foss21.org is the official repository per upstream
DESCRIPTION="Surrogate ltdl implementation"
HOMEPAGE="https://dev.midipix.org/cross/sltdl"

if [[ ${PV} == *9999 ]] ; then
	inherit git-r3
	EGIT_REPO_URI="https://dev.midipix.org/cross/sltdl"
else
	VERIFY_SIG_OPENPGP_KEY_PATH=/usr/share/openpgp-keys/midipix.asc
	inherit verify-sig

	SRC_URI="https://dl.foss21.org/${PN}/${P}.tar.xz"
	SRC_URI+=" verify-sig? ( https://dl.foss21.org/${PN}/${P}.tar.xz.sig )"

	KEYWORDS="~amd64"
fi

BDEPEND="sys-devel/m4"

if [[ ${PV} != *9999 ]] ; then
	BDEPEND+=" verify-sig? ( >=sec-keys/openpgp-keys-midipix-20250517 )"
fi

LICENSE="MIT"
SLOT="0"

src_configure() {
	# Custom configure script (not generated by autoconf)
	edo ./configure \
		--disable-static \
		--compiler="$(tc-getCC)" \
		--host=${CHOST} \
		--prefix="${EPREFIX}"/usr \
		--docdir="${ERPEFIX}"/usr/share/doc/${PF} \
		--mandir="${EPREFIX}"/usr/share/man \
		--libdir="${EPREFIX}/usr/$(get_libdir)"
}
